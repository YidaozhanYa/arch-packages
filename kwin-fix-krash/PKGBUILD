# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kwin
pkgname=kwin-fix-krash
pkgver=5.26.1
pkgrel=2
pkgdesc='An easy to use, but flexible, composited Window Manager - fix Krash bug.'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils breeze
         pipewire-session-manager libqaccessibilityclient lcms2 libxcvt)
makedepends=(extra-cmake-modules qt5-tools kdoctools krunner wayland-protocols plasma-wayland-protocols python)
optdepends=('maliit-keyboard: virtual keyboard for kwin-wayland')
replaces=(kwayland-server kwin)
conflicts=(kwin)
provides=(kwin)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$_pkgname-$pkgver.tar.xz
0001-Make-damage-region-fetching-code-more-robust-to-errors.patch)
install=$_pkgname.install
sha256sums=('c3acc4b7ada4191780ea64f03ed9fa249a0c17af0040c0fc29d6bd6aeb8f7fa3'
            '2f3766f066d328c2a7a97e6092072ef9cc8f0fde10c68af98711be7a3dc659f2')

prepare(){
  patch -Rp0 "${srcdir}/kwin-5.26.1/src/surfaceitem_x11.cpp" < 0001-Make-damage-region-fetching-code-more-robust-to-errors.patch
}

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
